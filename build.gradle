plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.0.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.15' apply false
	id 'io.gitlab.arturbosch.detekt' version '1.23.8' apply false
}

group = 'com.bookmanagementsystem'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation project(':presentation')
	implementation project(':infrastructure')
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
	mainClass = 'com.bookmanagementsystem.BookmanagementsystemApplicationKt'
}

// NOTE: .env.sample を参照して .env ファイルを作成してください
def properties = new Properties()
def envFile = rootProject.file(".env")
if (envFile.exists()) {
    properties.load(new FileInputStream(envFile))
}

def postgresUser = System.getenv("DB_USERNAME") ?: properties.getProperty("DB_USERNAME")
def postgresPassword = System.getenv("DB_PASSWORD") ?: properties.getProperty("DB_PASSWORD")

bootRun {
	environment 'DB_URL', 'jdbc:postgresql://localhost:5433/book_management'
	environment 'DB_USERNAME', postgresUser
	environment 'DB_PASSWORD', postgresPassword
}

subprojects {
	apply plugin: 'org.jetbrains.kotlin.jvm'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'io.gitlab.arturbosch.detekt'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
		detektPlugins 'io.gitlab.arturbosch.detekt:detekt-formatting:1.23.8'
	}

	kotlin {
		compilerOptions {
			freeCompilerArgs.addAll '-Xjsr305=strict'
		}
	}

	detekt {
		config.setFrom(file("$rootProject.projectDir/detekt.yml"))
		buildUponDefaultConfig = true
		parallel = true
		
		reports {
			html.required = true
			xml.required = true
		}
	}
}
